// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract DemoToken {
  string public name;
  string public symbol;
  // forge-lint: disable-next-line(screaming-snake-case-const)
  uint8 public constant decimals = 18;
  uint256 public totalSupply;

  mapping(address => uint256) public balanceOf;
  mapping(address => mapping(address => uint256)) public allowance;

  event Transfer(address indexed from, address indexed to, uint256 value);
  event Approval(address indexed owner, address indexed spender, uint256 value);

  constructor(string memory name_, string memory symbol_, address to, uint256 supply) {
    name = name_;
    symbol = symbol_;
    _mint(to, supply);
  }

  function transfer(address to, uint256 value) external returns (bool) {
    _transfer(msg.sender, to, value);
    return true;
  }

  function approve(address spender, uint256 value) external returns (bool) {
    allowance[msg.sender][spender] = value;
    emit Approval(msg.sender, spender, value);
    return true;
  }

  function transferFrom(address from, address to, uint256 value) external returns (bool) {
    uint256 allowed = allowance[from][msg.sender];
    require(allowed >= value, "ALLOWANCE");
    if (allowed != type(uint256).max) {
      allowance[from][msg.sender] = allowed - value;
    }
    _transfer(from, to, value);
    return true;
  }

  function _transfer(address from, address to, uint256 value) internal {
    require(to != address(0), "ZERO_ADDR");
    uint256 bal = balanceOf[from];
    require(bal >= value, "BALANCE");
    balanceOf[from] = bal - value;
    balanceOf[to] += value;
    emit Transfer(from, to, value);
  }

  function _mint(address to, uint256 value) internal {
    totalSupply += value;
    balanceOf[to] += value;
    emit Transfer(address(0), to, value);
  }
}
